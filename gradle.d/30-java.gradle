project.ext {
    MongoIntegrationTests = 'com.github.reflectoring.infiniboard.test.categories.MongoIntegrationTests'
}

subprojects {

    if (file('src/main/java').exists()) {
        apply plugin: 'jacoco'
        apply plugin: 'java'

        repositories {
            jcenter()
        }

        dependencies {
            testCompile (
                    project(':test-base'),
            )
        }

        jacoco {
            toolVersion = version_jacoco
        }

        compileJava {
            sourceCompatibility = version_java
            targetCompatibility = version_java

            options.fork = true
        }

        test {
            useJUnit {
                // exclude all categories as default
                excludeCategories project.MongoIntegrationTests
            }

            // set heap size for the test JVM(s)
            minHeapSize = "128m"
            maxHeapSize = "1024m"

            jacoco {
                append = false
                includes = [
                        "com/github/**/*"
                ]
            }
        }

    } else {
        apply plugin: 'base'
    }

}
