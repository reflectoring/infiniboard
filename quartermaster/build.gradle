import org.apache.tools.ant.filters.ReplaceTokens

import java.text.SimpleDateFormat

buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${version_springBoot}"
        classpath "org.springframework:springloaded:${version_springLoaded}"
        classpath "com.sourcemuse.gradle.plugin:gradle-mongo-plugin:${version_mongo_plugin}"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:${version_asciidoctor_plugin}"
        classpath "org.ajoberstar:gradle-git-publish:${version_git_publish}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${version_bintray_plugin}"
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'mongo'
apply plugin: 'war'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'org.ajoberstar.git-publish'
apply plugin: 'com.jfrog.bintray'

apply from: '../gradle.d/02-docker.gradle'
apply from: '../gradle.d/30-java.gradle'

dependencies {
    compile(
            project(":packrat"),
            "org.springframework.boot:spring-boot-starter-actuator:${version_springBoot}",
            "org.springframework.boot:spring-boot-starter-hateoas:${version_springBoot}"
    )

    compileOnly(
            "org.springframework.boot:spring-boot-starter-tomcat:${version_springBoot}"
    )

    testCompile(
            "org.springframework.boot:spring-boot-starter-test:${version_springBoot}",
            "org.springframework.restdocs:spring-restdocs-mockmvc:${version_springRestDocs}",
            "org.springframework.restdocs:spring-restdocs-core:${version_springRestDocs}",
            "com.jayway.jsonpath:json-path:${version_jsonPath}",
            "junit:junit:${version_junit}"
    )
}

task integrationTest(type: Test) {
    runWithMongoDb = true // activates mongo plugin to start embedded MongoDB
    useJUnit {
        includeCategories project.MongoIntegrationTests
    }
}

bootRepackage {
    mainClass = 'com.github.reflectoring.Quartermaster'
    executable = true
}

bootRun {
    // default application configuration for running application via bootRun in development mode
    // (in production, these properties are defined in application.properties)
    jvmArgs '-Xdebug',
            '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=9009',
            '-Dspring.output.ansi.enabled=ALWAYS'
}

processResources {
    filter ReplaceTokens, tokens: [
            "infiniboard.version": version,
            "infiniboard.majorVersion": majorVersion,
            "infiniboard.minorVersion": minorVersion,
            "infiniboard.patchVersion": patchVersion
    ]
}

war {
    // include AngularJS client into war,
    // if it has been already build
    from('../dashy/dist')
}

ext {
    snippetsDir = file('build/generated-snippets')
}

asciidoctor {
    attributes 'snippets': snippetsDir,
            'version': version,
            'date': new SimpleDateFormat("yyyy-MM-dd").format(new Date()),
            'stylesheet': 'themes/github.css'
    inputs.dir snippetsDir
    dependsOn test
    sourceDir 'src/main/asciidoc'
    outputDir 'build/doc/restapi'
}

task preparePages() {
    doLast {
        copy {
            from "${project.buildDir}/doc/restapi/html5"
            into "${project.buildDir}/pages/restapi"
        }
    }
}

preparePages.dependsOn asciidoctor
build.dependsOn preparePages

gitPublish {
    // username must be stored in environment variable  GRGIT_USER
    // api token must be stored in environment variable GRGIT_PASS
    repoUri = "https://github.com/reflectoring/infiniboard.git"

    branch = 'gh-pages'

    contents {
        from "${project.buildDir}/pages"
        into "docs/${baseVersion}"
    }

    preserve { include '**/*' }

    commitMessage "[skip ci] updated docs for ${baseVersion}"
}

gitPublishCopy.dependsOn preparePages

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    filesSpec {
        into '.'
        from('build/libs') {
            include "${project.name}-${project.version}.war"
        }
    }

    publish = true

    pkg {
        userOrg = 'reflectoring'
        repo = 'releases'
        name = 'infiniboard'
        desc = 'official infiniboard release repository'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/reflectoring/infiniboard.git'
        websiteUrl = 'http://reflectoring.github.io/infiniboard/'

        publicDownloadNumbers = true

        version {
            name = project.baseVersion
            released = new Date()
        }
    }
}

bintrayUpload.dependsOn build
